(($) ->

  ###
  Uploader jQuery Plugins
  ###

  $.fn.addDanger = ->
    @addClass('bg-danger')
  $.fn.removeDanger = ->
    @removeClass('bg-danger')

  $.fn.getUploadId = ->
    parseInt(@data('upload-id'))
  $.fn.getThumbnail = ->
    @find('[v-thumbnail]')
  $.fn.getRemoveButton = ->
    @find('[v-remove-button]')
  $.fn.getProgressBar = ->
    @find('[v-progress-bar]')

  $.fn.markAsComplete = ->
    @addClass('upload-item-complete')

  $.fn.setProgress = (percentage) ->
    progress = parseInt(percentage)
    progress = 0 if isNaN(progress) || progress < 0
    $progressBar = @getProgressBar()
    $progressBar.attr('aria-valuenow', progress)
    $progressBar.attr('style', "width: #{progress}%;")

  $.fn.setTitle = (title) ->
    @find('[v-title]').text(title)
    return @

  $.fn.setupRemoveButton = (uploader) ->
    @getRemoveButton().on 'click', ((e) =>
      e.preventDefault()

      removeUpload = (uploadId) ->
        upload = uploader.getUploads({ id: uploadId })
        if upload.status == qq.status.UPLOAD_SUCCESSFUL or
           upload.status == qq.status.DELETE_FAILED
          uploader.deleteFile(uploadId)
        else if upload.status == qq.status.UPLOADING or
                upload.status == qq.status.CANCELED
          uploader.cancel(uploadId)

      uploadId = @getUploadId()
      removeUpload(uploadId)
      thumbnails = uploader.getUploads().filter((item) ->
        item.parentId == uploadId
      )
      removeUpload(thumbnail.id) for thumbnail in thumbnails

      return false
    )
    return @

  $.fn.setupThumbnail = (uploader) ->
    $thumbnail = @getThumbnail()
    $thumbnail.load (=>
      # only display originals
      # workaround to fix scaling.hideScaled = true
      @show() if uploader.getParentId(@getUploadId()) == null
    )
    uploadId = @getUploadId()
    img = $thumbnail.get(0)

    uploader.drawThumbnail(uploadId, img, <%= PhotoService.thumbnail_size %>, false, (resizeInfo) ->
      new Promise((resolve, reject) ->
        options =
          unsharpAmount: 80
          unsharpRadius: 0.6
          unsharpThreshold: 2
        pica().resize(resizeInfo.sourceCanvas, resizeInfo.targetCanvas, options).then((result) ->
          resolve()
        )
      )
    )
    return @

  ###
  Application
  ###

  app = undefined
  app = window.Emporium or (window.Emporium = {})
  app._uploader =

    ###
    Uploader
    ###

    findItem: (uploadId) ->
      @items ||= {}
      $item = @items[uploadId] || $("#upload_item_#{uploadId}")
      @items[uploadId] ||= $item if $item.length > 0
      return $item

    createItem: (uploadId, name) ->
      @itemTemplate ||= $('#upload-item-template').html()
      @$itemContainer ||= $('#upload-items')
      @$itemContainer.append(@itemTemplate.replace(/{{uploadId}}/g, uploadId))
      return @findItem(uploadId)

    uploadStart: (uploadId, filename) ->
      $item = @findItem(uploadId)
      # retry
      if $item.length > 0
        $item.getProgressBar().removeDanger()
      # new upload
      else
        $item = @createItem(uploadId)
          .setTitle(filename)
          .setupRemoveButton(@uploader)
          .setupThumbnail(@uploader)
      return true

    uploadProgress: (uploadId, percentage) ->
      $item = @findItem(uploadId)
      $item.setProgress(percentage)

    thumbnailsUploadComplete: (uploadId) ->
      thumbnails = @uploader.getUploads().filter((item) ->
        item.parentId == uploadId
      )
      uploadedThumbnails = thumbnails.filter((item) ->
        item.status == qq.status.UPLOAD_SUCCESSFUL
      )
      return thumbnails.length == uploadedThumbnails.length

    uploadComplete: (uploadId) ->
      upload = @uploader.getUploads(id: uploadId)
      upload = @uploader.getUploads(id: upload.parentId) if upload.parentId
      if upload.status == qq.status.UPLOAD_SUCCESSFUL and @thumbnailsUploadComplete(upload.id)
        $item = @findItem(upload.id)
        $item.markAsComplete()
      # retry all on the queue if a upload was completed successfully
      @retryAll()
      return true

    uploadError: (uploadId) ->
      $item = @findItem(uploadId)
      $item.getProgressBar().addDanger()
      return true

    uploadCancelOrDelete: (uploadId) ->
      upload = @uploader.getUploads(id: uploadId)
      if upload.status == qq.status.UPLOADING or
         upload.status == qq.status.CANCELED
        $item = @findItem(uploadId)
        $item.hide() if $item.is(':visible')
      return true

    uploadDeleteComplete: (uploadId) ->
      upload = @uploader.getUploads(id: uploadId)
      if upload.status == qq.status.DELETED
        $item = @findItem(uploadId)
        $item.hide() if $item.is(':visible')
      return true

    # https://blog.fineuploader.com/2013/08/16/fine-uploader-s3-upload-directly-to-amazon-s3-from-your-browser/#client-side-integration
    initializeUploader: (uploadButton) ->
      @uploader = new qq.s3.FineUploaderBasic
        debug: <%= Rails.env.development? %>
        button: uploadButton
        request:
          endpoint: "<%= StorageService.endpoint %>"
          accessKey: "<%= StorageService.access_key_id %>"
        objectProperties:
          key: (fileId) =>
            filename = @uploader.getName(fileId)
            uuid = @uploader.getUuid(fileId)
            ext = filename.substr(filename.lastIndexOf('.') + 1)
            "photos/#{uuid}.#{ext}"
        signature:
          endpoint: '/uploads/sign'
          version: 4
        uploadSuccess:
          endpoint: '/photos'
        deleteFile:
          enabled: true
          endpoint: '/photos'
        retry:
          enabledAuto: true
        resume:
          enabled: true
        chunking:
          enabled: true
          partSize: 5242880
          # Upload big chunks one at time. In case of failure, just one chunk will be lost
          # concurrent:
          #   enabled: true
        # paste:
        #   targetElement: element
        #   promptForName: true
        validation:
          allowedExtensions: ["jpeg", "jpg"]
          acceptFiles: "image/jpeg"
        scaling:
          sizes: <%= PhotoService.uploader_sizes.to_json %>
          customResizer: (resizeInfo) ->
            @__targetCanvas = resizeInfo.targetCanvas
            new Promise((resolve, reject) =>
              options =
                unsharpAmount: 80
                unsharpRadius: 0.6
                unsharpThreshold: 2
              pica().resize(resizeInfo.sourceCanvas, resizeInfo.targetCanvas, options).then((result) =>
                # TODO improve watermark
                ctx=@__targetCanvas.getContext('2d')
                ctx.font = "12px serif"
                ctx.fillStyle = "red"
                ctx.fillText("EMPORIUM" + Date().toString(),10, 10)
                resolve()
              , reject)
            )
          # # not working
          # # workaround: @show() if uploader.getParentId(@getUploadId()) == null
          # hideScaled: true
        callbacks:
          onProgress: (uploadId, name, uploadedBytes, totalBytes) =>
            @uploadProgress(uploadId, uploadedBytes/totalBytes*100)
          onUpload: (uploadId, name) =>
            @uploadStart(uploadId, name)
          onComplete: (uploadId, name, responseJSON, xhr) =>
            if responseJSON['success']
              @uploadComplete(uploadId)
            else
              @uploadError(uploadId)
          onError: (uploadId, name, errorReason, xhr) =>
            @uploadError(uploadId)
          onCancel: (uploadId) =>
            @uploadCancelOrDelete(uploadId)
          onDelete: (uploadId) =>
            @uploadCancelOrDelete(uploadId)
          onDeleteComplete: (uploadId, xhr, isError) =>
            @uploadDeleteComplete(uploadId)
          onError: (uploadId, name, errorReason, xhr) =>
            @uploadError(uploadId)
          onStatusChange: (uploadId, oldStatus, newStatus) =>
            if newStatus == qq.status.UPLOAD_FAILED
              @addToRetryQueue(uploadId)

      @initializeRetryQueue()

    ###
    Retry Queue
    ###

    addToRetryQueue: (uploadId) ->
      @retryQueue.push(uploadId)

    retryUpload: ->
      uploadId = @retryQueue.shift()
      @uploader.retry(uploadId) unless uploadId == undefined
      setTimeout (=>
        @monitorRetryQueue()
      ), 5000

    monitorRetryQueue: ->
      setTimeout (=>
        @retryUpload()
      ), 5000

    retryAll: ->
      @uploader.retry(@retryQueue.shift()) while @retryQueue.length > 0

    initializeRetryQueue: ->
      @retryQueue = []
      @monitorRetryQueue()

  app._uploader
) jQuery
